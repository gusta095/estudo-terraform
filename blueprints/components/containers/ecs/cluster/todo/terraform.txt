variable "cluster_name" {
  description = "Nome do cluster ECS"
  type        = string
}

variable "service_name" {
  description = "Nome do serviço ECS a ser gerenciado"
  type        = string
}

variable "fargate_start_schedule_expression" {
  description = "Expressão cron para iniciar as tasks do Fargate. Deixar vazio para desativar."
  type        = string
  default     = ""
}

variable "fargate_stop_schedule_expression" {
  description = "Expressão cron para parar as tasks do Fargate. Deixar vazio para desativar."
  type        = string
  default     = ""
}

resource "aws_cloudwatch_event_rule" "start_task" {
  count       = var.fargate_start_schedule_expression != "" ? 1 : 0

  name        = "${var.cluster_name}-start-task"
  description = "Agendamento para iniciar tasks no cluster Fargate"
  schedule_expression = var.fargate_start_schedule_expression
}

resource "aws_cloudwatch_event_rule" "stop_task" {
  count       = var.fargate_stop_schedule_expression != "" ? 1 : 0

  name        = "${var.cluster_name}-stop-task"
  description = "Agendamento para parar tasks no cluster Fargate"
  schedule_expression = var.fargate_stop_schedule_expression
}

resource "aws_cloudwatch_event_target" "start_task" {
  count     = var.fargate_start_schedule_expression != "" ? 1 : 0

  rule      = aws_cloudwatch_event_rule.start_task[0].name
  target_id = "start-task"
  arn       = aws_lambda_function.ecs_task_control.arn
  input     = jsonencode({ action = "start", cluster = var.cluster_name, service = var.service_name })
}

resource "aws_cloudwatch_event_target" "stop_task" {
  count     = var.fargate_stop_schedule_expression != "" ? 1 : 0

  rule      = aws_cloudwatch_event_rule.stop_task[0].name
  target_id = "stop-task"
  arn       = aws_lambda_function.ecs_task_control.arn
  input     = jsonencode({ action = "stop", cluster = var.cluster_name, service = var.service_name })
}

resource "aws_lambda_function" "ecs_task_control" { # usar a minha blueprint
  function_name = "ecs-task-control"
  runtime       = "python3.9"
  handler       = "lambda_function.lambda_handler"
  role          = aws_iam_role.lambda_exec.arn
  filename      = "lambda.zip"
}
